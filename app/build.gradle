apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion '25.0.0'
    defaultConfig {
        applicationId "com.sctdroid.app.textemoji"
        minSdkVersion 19
        targetSdkVersion 24
        versionCode 3
        versionName "1.1 CashS alpha-2017.6.7"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        sctdroid {
            keyAlias 'SctDroidKey'
            keyPassword project.hasProperty("KEY_PASSWORD") ? KEY_PASSWORD : System.getenv("KEY_PASSWORD")
            storeFile file('../sctdroid.jks')
            storePassword project.hasProperty("KEYSTORE_PASSWORD") ? KEYSTORE_PASSWORD : System.getenv("KEYSTORE_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.sctdroid
        }
        debug {
            signingConfig signingConfigs.sctdroid
        }
    }

    productFlavors {
        yingyongbao {}
        _360 {}
        chuizi {}
        wandoujia {}
        mi {}
        github {}
        googleplay {}
    }

    productFlavors.all { flavor ->
        if (name.contains('googleplay')) {
            flavor.manifestPlaceholders = [Your_channel_id: 'play.google.com']
        } else {
            flavor.manifestPlaceholders = [Your_channel_id: name]
        }
        flavor.manifestPlaceholders.put('TENOR_API_KEY', project.hasProperty("TENOR_API_KEY") ? TENOR_API_KEY : System.getenv("TENOR_API_KEY"))
        flavor.manifestPlaceholders.put('SOOGIF_SALT', project.hasProperty("SOOGIF_SALT") ? SOOGIF_SALT : System.getenv("SOOGIF_SALT"))
    }

    packagingOptions{
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], excludes: ['GP_*.jar', 'TalkingData*.jar'], dir: 'libs')
    googleplayCompile files('libs/GP_TalkingData_Analytics_Android_SDK_V2.2.37.jar')
    yingyongbaoCompile files('libs/TalkingData_AppAnalytics_Android_SDK_V2.3.62.jar')
    _360Compile files('libs/TalkingData_AppAnalytics_Android_SDK_V2.3.62.jar')
    chuiziCompile files('libs/TalkingData_AppAnalytics_Android_SDK_V2.3.62.jar')
    wandoujiaCompile files('libs/TalkingData_AppAnalytics_Android_SDK_V2.3.62.jar')
    miCompile files('libs/TalkingData_AppAnalytics_Android_SDK_V2.3.62.jar')
    githubCompile files('libs/TalkingData_AppAnalytics_Android_SDK_V2.3.62.jar')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:cardview-v7:24.2.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile ('cn.leancloud.android:avoscloud-feedback:v4.1.0@aar')
    compile ('cn.leancloud.android:avoscloud-sdk:v4.1.0')
}
